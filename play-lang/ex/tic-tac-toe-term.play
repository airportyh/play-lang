def main() [
    while true [
        playGame()
        answer = prompt("Play again? (y or n)")
        if answer == "n" [
            break
        ]
    ]
]

def playGame() [
    currentPlayer = "O"
    gameOver = false
    board = [
        " ", " ", " ", 
        " ", " ", " ",
        " ", " ", " "
    ]
    
    while true [
        drawBoard(board)
        humanMove(board, currentPlayer)
        
        if isWinner(board, currentPlayer) [
            print("The winner is " + currentPlayer)
            gameOver = true
        ] else [
            currentPlayer = switchPlayer(currentPlayer)
        ]
        
        if gameOver [
            break
        ]
        
        if isDraw(board) [
            gameOver = true
            print("It's a draw")
            break
        ]
        
        sleep(1)
        
        # Computer moves
        smartComputer(board, currentPlayer)
        if isWinner(board, currentPlayer) [
            drawBoard(board)
            print("The winner is " + currentPlayer)
            gameOver = true
        ] else [
            currentPlayer = switchPlayer(currentPlayer)
        ]
        
        if gameOver [
            break
        ]
        
    ]
]

def humanMove(board, currentPlayer) [
    while true [
        move = parseNumber(prompt("Your move? (1-9)"))
        if isNaN(move) or move < 1 or move > 9  [
            print("I don't understand. Please choose a move between 1 and 9.")
            continue
        ]
        idx = move - 1
        if board[idx] != " " [
            print("That square is taken. Choose again.")
            continue
        ]
        board[idx] = currentPlayer
        break
    ]
]

def naiveComputer(board, currentPlayer) [
    # choose a random open square

    openSquares = []
    i = 0
    while i < 9 [
        state = board[i]
        if state == " " [
            push(openSquares, i)
        ]
        i = i + 1
    ]
    idx = floor(random() * length(openSquares))
    choice = openSquares[idx]
    board[choice] = currentPlayer
]

def notDumbComputer(board, me) [
    # If I can win, win it
    i = 0
    while i < 9 [
        sx = getX(i)
        sy = getY(i)
        state = board[i]
        if state == " " [
            # try taking this square and see if I win
            board[i] = me
            if isWinner(board, me) [
                return
            ] else [
                # Revert the move
                board[i] = " "
            ]
        ]
        i = i + 1
    ]
    
    # If they can win, block it
    i = 0
    while i < 9 [
        sx = getX(i)
        sy = getY(i)
        state = board[i]
        if state == " " [
            # try letting them take this square
            opponent = switchPlayer(me)
            board[i] = opponent
            if isWinner(board, opponent) [
                board[i] = me
                return
            ] else [
                # Revert the move
                board[i] = " "
            ]
        ]
        i = i + 1
    ]
    
    # Default to naive completer
    naiveComputer(board, me)
]

def smartComputer(board, me) [
    start = currentTime()
    result = smartComputerCalculateMove(board, me, 3, false)
    end = currentTime()
    print("Computer took " + (end - start) + "ms to make this move.")
    move = result["move"]
    board[move] = me
]

# Score is either 0 - losing, 1 - even, or 2 - winning
def smartComputerCalculateMove(board, me, depth, min) [
    if isWinner(board, "X") [
        return { move: null, score: 2 }
    ]
    if isWinner(board, "O") [
        return { move: null, score: 0 }
    ]
    if depth == 0 [
        return smartComputerRandomMove(board, me)
    ]
    
    bestScore = null
    if min [
        bestScore = 3
    ] else [
        bestScore = -1
    ]
    bestMove = null
    opponent = switchPlayer(me)
    i = 0
    while i < 9 [
        if board[i] == " " [
            newBoard = imagineMove(board, i, me)
            result = smartComputerCalculateMove(newBoard, opponent, depth - 1, !min)
            
            if min [
                if result["score"] < bestScore [
                    bestScore = result["score"]
                    bestMove = i
                ]
            ] else [
                if result["score"] > bestScore [
                    bestScore = result["score"]
                    bestMove = i
                ]
            ]
        ]
        i = i + 1
    ]
    
    if bestScore == -1 [
        return smartComputerRandomMove(board, me)
    ] else [
        return { move: bestMove, score: bestScore }
    ]
]

def smartComputerRandomMove(board, me) [
    openSquares = []
    i = 0
    while i < 9 [
        state = board[i]
        if state == " " [
            push(openSquares, i)
        ]
        i = i + 1
    ]
    idx = floor(random() * length(openSquares))
    return { move: openSquares[idx], score: 1 }
]

def imagineMove(board, move, player) [
    newBoard = []
    i = 0
    while i < 9 [
        if i == move [
            push(newBoard, player)
        ] else [
            push(newBoard, board[i])
        ]
        i = i + 1
    ]
    return newBoard
]

def getX(i) [
    return 100 + (i % 3) * 100
]

def getY(i) [
    return 100 + floor(i / 3) * 100
]

def isDraw(board) [
    for state in board [
        if state == " " [
            return false
        ]
    ]
    return true
]

def isWinner(board, player) [
    zero = board[0] == player
    one = board[1] == player
    two = board[2] == player
    three = board[3] == player
    four = board[4] == player
    five = board[5] == player
    six = board[6] == player
    seven = board[7] == player
    eight = board[8] == player
    a = zero and one and two
    b = three and four and five
    c = six and seven and eight
    d = zero and four and eight
    e = two and four and six
    f = zero and three and six
    g = one and four and seven
    h = two and five and eight
    return a or b or c or d or e or f or g or h
]

def switchPlayer(player) [
    if player == "O" [
        return "X"
    ] else [
        return "O"
    ]
]

def drawBoard(board) [
    print("")
    print(board[0] + board[1] + board[2])
    print(board[3] + board[4] + board[5])
    print(board[6] + board[7] + board[8])
    print("")
]