def main() [
    clear()
    setFont("100px sans-serif")
    currentPlayer = "O"
    board = [
        { x: 100, y: 100, state: "" },
        { x: 200, y: 100, state: "" },
        { x: 300, y: 100, state: "" },
        { x: 100, y: 200, state: "" },
        { x: 200, y: 200, state: "" },
        { x: 300, y: 200, state: "" },
        { x: 100, y: 300, state: "" },
        { x: 200, y: 300, state: "" },
        { x: 300, y: 300, state: "" }
    ]
    drawBoard(board)
    while true [
        click = waitForEvent("click")
        x = click["x"]
        y = click["y"]
        
        for square in board [
            sx = square["x"]
            sy = square["y"]
            state = square["state"]
            if x > sx and x < sx + 90 and y > sy and y < sy + 90 [
                if state == "" [
                    square["state"] = currentPlayer
                    setFont("100px sans-serif")
                    setColor("black")
                    drawText(currentPlayer, sx + 5, sy)
                    if isWinner(board, currentPlayer) [
                        setFont("60px sans-serif")
                        drawText("The winner is " + currentPlayer, 0, 0)
                    ] else [
                        currentPlayer = switchPlayer(currentPlayer)
                    ]
                ]
            ]
        ]
    ]
]

def isWinner(board, player) [
    zero = board[0]["state"] == player
    one = board[1]["state"] == player
    two = board[2]["state"] == player
    three = board[3]["state"] == player
    four = board[4]["state"] == player
    five = board[5]["state"] == player
    six = board[6]["state"] == player
    seven = board[7]["state"] == player
    eight = board[8]["state"] == player
    a = zero and one and two
    b = three and four and five
    c = six and seven and eight
    d = zero and four and eight
    e = two and four and six
    f = zero and three and six
    g = one and four and seven
    h = two and five and eight
    return a or b or c or d or e or f or g or h
]

def switchPlayer(player) [
    if player == "O" [
        return "X"
    ] else [
        return "O"
    ]
]

def drawBoard(board) [
    setColor("#dddddd")

    for square in board [
        x = square["x"]
        y = square["y"]
        fillRect(x, y, 90, 90)
    ]
]